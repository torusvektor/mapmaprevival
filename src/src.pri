QT += gui
QT += opengl
QT += xml
QT += core
QT += network
QT += multimedia

greaterThan(QT_MAJOR_VERSION, 4) {
  QT -= gui # using widgets instead gui in Qt5
  QT += widgets
  # webenginewidgets is heavy and not always available: use it only if installed
  # (never on Windows), otherwise the shortcut window uses QTextBrowser.
  !win32:qtHaveModule(webenginewidgets) {
    QT += webenginewidgets
  } else {
    DEFINES += NO_WEBENGINE
  }
}

#Includes common configuration for all subdirectory .pro files.
INCLUDEPATH += $$PWD/core \
    $$PWD/shape \
    $$PWD/gui \
    $$PWD/control \
    $$PWD/app

#Linux-specific:
unix:!macx {
  DEFINES += UNIX
  CONFIG += link_pkgconfig
  INCLUDE_PATH +=
  PKGCONFIG += \
    gstreamer-1.0 gstreamer-base-1.0 gstreamer-app-1.0 gstreamer-pbutils-1.0 \
    gl x11
  QMAKE_CXXFLAGS_WARN_ON += -Wno-unused-result -Wno-unused-parameter \
                            -Wno-unused-variable -Wno-switch -Wno-comment \
                            -Wno-unused-but-set-variable
}

# macOS-specific:
macx {
  TARGET = MapMap
  DEFINES += MACOSX
  QMAKE_CXXFLAGS += -D__MACOSX_CORE__
  QMAKE_CXXFLAGS += -stdlib=libc++
  exists(/Library/Frameworks/GStreamer.framework) {
    # Official GStreamer framework installer.
    INCLUDEPATH += /Library/Frameworks/GStreamer.framework/Versions/1.0/Headers
    LIBS += -F /Library/Frameworks/ -framework GStreamer
  } else {
    # GStreamer from Homebrew or MacPorts, found with pkg-config.
    CONFIG += link_pkgconfig
    PKGCONFIG += gstreamer-1.0 gstreamer-base-1.0 gstreamer-app-1.0 gstreamer-pbutils-1.0
  }
  LIBS += -framework OpenGL -framework GLUT
  # With Xcode Tools > 1.5, to reduce the size of your binary even more:
  # LIBS += -dead_strip
  # This tells qmake not to put the executable inside a bundle.
  # just for reference. Do not uncomment.
  # CONFIG-=app_bundle
  ICON = resources/app_icons/mapmap.icns
}


# Windows-specific:
win32 {
  DEFINES += WIN32
  TARGET = MapMap

  # GStreamer paths - try environment variable first, then fallback to common paths
  GST_HOME = $$quote($$(GSTREAMER_1_0_ROOT_MSVC_X86_64))
  isEmpty(GST_HOME) {
    # Fallback to development installation path
    GST_HOME = C:/gstreamer/1.0/msvc_x86_64/1.0/msvc_x86_64
    !exists($$GST_HOME/include/gstreamer-1.0) {
      GST_HOME = C:/gstreamer/1.0/msvc_x86_64
      !exists($$GST_HOME/include/gstreamer-1.0) {
        error("GStreamer not found. Please install GStreamer development package.")
      }
    }
  }
  message("GStreamer detected in: $${GST_HOME}")

  INCLUDEPATH += $${GST_HOME}/include/gstreamer-1.0 \
    $${GST_HOME}/include/glib-2.0 \
    $${GST_HOME}/lib/glib-2.0/include \
    $${GST_HOME}/include

  LIBS += $${GST_HOME}/lib/gstapp-1.0.lib \
    $${GST_HOME}/lib/gstbase-1.0.lib \
    $${GST_HOME}/lib/gstpbutils-1.0.lib \
    $${GST_HOME}/lib/gstreamer-1.0.lib \
    $${GST_HOME}/lib/gobject-2.0.lib \
    $${GST_HOME}/lib/glib-2.0.lib \
    $${GST_HOME}/lib/gstaudio-1.0.lib \
    $${GST_HOME}/lib/gstvideo-1.0.lib \
    opengl32.lib

  CONFIG -= debug
  CONFIG += release

  RC_FILE = resources/windows_resource.rc
  QMAKE_CXXFLAGS += -D_USE_MATH_DEFINES

  # Suppress some warnings
  QMAKE_CXXFLAGS_WARN_ON += -wd4996 -wd4267 -wd4244
}
